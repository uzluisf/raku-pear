use Pear;
use Pear::Config;
 
=comment APP SETUP
 
# Allow that named arguments appear after positional ones.
# See: https://docs.raku.org/language/create-cli#%*SUB-MAIN-OPTS
my %*SUB-MAIN-OPTS = :named-anywhere;
 
# Set up the directory and the configuration file.
my $work-dir = $*CWD.absolute;
my $config   = Pear::Config.get-config($work-dir);
 
# Set up the application.
my $app = Pear.new(:$work-dir, :$config);
 
=comment CLI OPTIONS
 
#| Render the site.
multi MAIN(
    'render',
    Bool:D :$skip-pages = False, #= Skip pages generation. (Default = False)
    Bool:D :$skip-blog  = False, #= Skip blog generation. (Default = False)
    Bool:D :$skip-tags  = False, #= Skip tags generation. (Default = False)
) {
    given $app {
        .generate-posts if !$skip-blog;
        .generate-pages if !$skip-pages;
        .generate-tags  if !$skip-tags;
    }
}

#| Start local web server.
multi MAIN(
    'serve',
    Int:D :p(:$port) = 3000, #= Port in which the server should run. (Default = 3000)
) {
    my $build-dir = $config.output-dir;
    my $server = Pear::Preview::HTTP.new(
        :$build-dir, :$port, :host('localhost'), :!no-livereload, :!omit-html-ext,
    );

    await $server.webserver();
}

#| Start local web server and re-render site on content/template modification.
multi MAIN(
    'watch',
    Int:D :p(:$port) = 3000,       #= Port in which the server should run. (Default = 3000)
    Bool:D :$no-livereload = False #= Disable livereload when running pear watch. (Default = False)
) {
    class Writer {
        has $.app;

        method write {
            given $!app {
                .generate-posts;
                .generate-pages;
            }
        }
    }

    my $writer  = Writer.new(:$app);
    my $watcher = Pear::Preview.new(
        :$writer, :$config, :$port, :host('localhost'), :$no-livereload
    );

    $watcher.watch()
}
